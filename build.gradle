// Top-level build file where you can add configuration options common to all sub-projects/modules.

ext {
    // NOTE: xxx.yy.zz; -SNAPSHOT; -RCxx
    VERSION_NAME = "0.2.9"
    SDK_VERSION = 26
    MIN_SDK_VERSION = 19
    BUILD_TOOL_VERSION = '26.0.1'

    multidexVersion = '1.0.2'
    supportLibraryVersion = '26.0.2'
    constraintLayoutVersion = '1.0.2'
    junitVersion = '4.12'
    espressoVersion = '2.2.2'
    guavaVersion = '23.0'
    daggerVersion = '2.11'
    jsr250ApiVersion = '1.0'
    mockitoVersion = '2.8.47'
    dexmakerVersion = '2.2.0'
    powermockVersion = '1.7.1'
    rxjavaVersion = '2.1.3'
    rxandroidVersion = '2.0.1'
    nextcloudLibraryVersion = '1.0.30'
    uiAutomatorVersion = '2.1.2'
    jsoupVersion = '1.10.3'
    stethoVersion = '1.5.0'
    // Resolve conflicts
    findbugsJsr305Version = '2.0.1'
}

ext {
    buildVersionCode = {
        // versionCode MAX: 2 100 000 000
        // Release: xxx.yy.zz.99
        // -SNAPSHOT: *.00
        // -RCxx: *.(1-98)
        def candidate = "99"
        def (major, minor, patch) = VERSION_NAME.toLowerCase().replaceAll('-', '').tokenize('.')
        if (patch.endsWith("snapshot")) {
            candidate = "0"
            patch = patch.replaceAll("[^0-9]", "");
        } else {
            def rc
            (patch, rc) = patch.tokenize("rc")
            if (rc) {
                candidate = rc
            }
        }
        (major, minor, patch, candidate) = [major, minor, patch, candidate].collect{it.toInteger()}
        (major * 1000000) + (minor * 10000) + (patch * 100) + candidate
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-beta5'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        // https://jitpack.io/docs/BUILDING/#gradle-projects
        // https://jitpack.io/com/github/USER/REPO/TAG/build.log
        maven { url 'https://maven.google.com' }
        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
