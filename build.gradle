// Top-level build file where you can add configuration options common to all sub-projects/modules.

ext {
    // NOTE: xxx.yy.zz; -SNAPSHOT; -RCxx
    VERSION_NAME = "0.0.1-SNAPSHOT"
    SDK_VERSION = 25
    MIN_SDK_VERSION = 24
    BUILD_TOOL_VERSION = '25.0.2'

    supportLibraryVersion = '25.3.1'
    constraintLayoutVersion = '1.0.2'
    junitVersion = '4.12'
    espressoVersion = '2.2.2'
    guavaVersion = '20.0' // NOTE: v21 requested Java1.7 compatibility
    daggerVersion = '2.9'
    jsr250ApiVersion = '1.0'
    mockitoVersion = '2.7.12'
    dexmakerVersion = '2.2.0'
    powermockVersion = '1.7.0RC4'
    rxjavaVersion = '2.0.6'
    rxandroidVersion = '2.0.1'
    nextcloudLibraryVersion = '1.0.11'
    uiAutomatorVersion = '2.1.2'
    daggerMockVersion = '0.6.4'
    jsoupVersion = '1.10.2'
}

ext {
    buildVersionCode = {
        // versionCode MAX: 2 100 000 000
        // Release: xxx.yy.zz.99
        // -SNAPSHOT: *.00
        // -RCxx: *.(1-98)
        def candidate = "99"
        def (major, minor, patch) = VERSION_NAME.toLowerCase().replaceAll('-', '').tokenize('.')
        if (patch.endsWith("snapshot")) {
            candidate = "0"
            patch = patch.replaceAll("[^0-9]", "");
        } else {
            def rc
            (patch, rc) = patch.tokenize("rc")
            if (rc) {
                candidate = rc
            }
        }
        (major, minor, patch, candidate) = [major, minor, patch, candidate].collect{it.toInteger()}
        (major * 1000000) + (minor * 10000) + (patch * 100) + candidate
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.1'
        classpath 'me.tatarka:gradle-retrolambda:3.5.0'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

//plugins {
//    id "me.tatarka.retrolambda" version "3.5.0"
//}

allprojects {
    repositories {
        jcenter()
        maven {
            url "https://jitpack.io"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
