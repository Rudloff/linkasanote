// Top-level build file where you can add configuration options common to all sub-projects/modules.

ext {
    // NOTE: xxx.yy.zz; -SNAPSHOT; -RCxx
    VERSION_NAME = "0.2.0-RC8"
    SDK_VERSION = 25
    MIN_SDK_VERSION = 19
    BUILD_TOOL_VERSION = '25.0.3'

    multidexVersion = '1.0.1'
    supportLibraryVersion = '25.3.1'
    constraintLayoutVersion = '1.0.2'
    junitVersion = '4.12'
    espressoVersion = '2.2.2'
    guavaVersion = '22.0'
    daggerVersion = '2.10'
    jsr250ApiVersion = '1.0'
    mockitoVersion = '2.8.9'
    dexmakerVersion = '2.2.0'
    powermockVersion = '1.7.0RC4'
    rxjavaVersion = '2.1.0'
    rxandroidVersion = '2.0.1'
    nextcloudLibraryVersion = '1.0.18'
    uiAutomatorVersion = '2.1.2'
    jsoupVersion = '1.10.2'
    stethoVersion = '1.5.0'
    // Resolve conflicts
    findbugsJsr305Version = '2.0.1'
}

ext {
    buildVersionCode = {
        // versionCode MAX: 2 100 000 000
        // Release: xxx.yy.zz.99
        // -SNAPSHOT: *.00
        // -RCxx: *.(1-98)
        def candidate = "99"
        def (major, minor, patch) = VERSION_NAME.toLowerCase().replaceAll('-', '').tokenize('.')
        if (patch.endsWith("snapshot")) {
            candidate = "0"
            patch = patch.replaceAll("[^0-9]", "");
        } else {
            def rc
            (patch, rc) = patch.tokenize("rc")
            if (rc) {
                candidate = rc
            }
        }
        (major, minor, patch, candidate) = [major, minor, patch, candidate].collect{it.toInteger()}
        (major * 1000000) + (minor * 10000) + (patch * 100) + candidate
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath "me.tatarka:gradle-retrolambda:3.6.1"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

//plugins {
//    id "me.tatarka.retrolambda" version "3.6.1"
//}

allprojects {
    repositories {
        jcenter()
        maven {
            url "https://jitpack.io"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
