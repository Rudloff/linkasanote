apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion SDK_VERSION
    buildToolsVersion BUILD_TOOL_VERSION

    defaultConfig {
        applicationId "com.bytesforge.linkasanote"
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion SDK_VERSION
        versionCode 1
        versionName "0.1"
        //testApplicationId "${applicationId}.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //jackOptions {
        //    enabled true
        //}
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled false
            useProguard false
        }
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
        }
        prod
    }

    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            showStandardStreams = true
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':tokenautocomplete')
    //compile project(':nextcloud')

    compile "com.android.support:appcompat-v7:${supportLibraryVersion}"
    compile "com.android.support:design:${supportLibraryVersion}"
    compile "com.android.support:preference-v14:${supportLibraryVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibraryVersion}"
    compile "com.android.support.test.espresso:espresso-idling-resource:${espressoVersion}"
    compile "com.android.support.constraint:constraint-layout:${constraintLayoutVersion}"
    compile "com.google.guava:guava:${guavaVersion}"
    compile "com.github.nextcloud:android-library:${nextcloudLibraryVersion}"

    // RxJava
    compile "io.reactivex:rxjava:${rxjavaVersion}"
    compile "io.reactivex:rxandroid:${rxandroidVersion}"
    compile "com.squareup.sqlbrite:sqlbrite:${sqlbriteVersion}"

    // Dagger 2
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    compile "com.google.dagger:dagger:${daggerVersion}"
    provided "javax.annotation:jsr250-api:${jsr250ApiVersion}"

    // Local unit tests
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
    testCompile "org.powermock:powermock:${powermockVersion}"
    testCompile "org.powermock:powermock-module-junit4:${powermockVersion}"
    testCompile "org.powermock:powermock-api-mockito:${powermockVersion}"

    // Espresso Testing
    androidTestCompile "com.android.support.test.espresso:espresso-core:${espressoVersion}"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:${espressoVersion}"
    androidTestCompile "org.mockito:mockito-core:${mockitoVersion}"
    androidTestCompile "com.google.dexmaker:dexmaker:${dexmakerVersion}"
    androidTestCompile "com.google.dexmaker:dexmaker-mockito:${dexmakerVersion}"
    androidTestCompile "com.android.support.test.uiautomator:uiautomator-v18:${uiAutomatorVersion}"

    // Resolve conflicts
    androidTestCompile "com.android.support:recyclerview-v7:${supportLibraryVersion}"
    androidTestCompile "com.android.support:support-v4:${supportLibraryVersion}"
    androidTestCompile "com.android.support:appcompat-v7:${supportLibraryVersion}"
    androidTestCompile "com.android.support:design:${supportLibraryVersion}"
}
