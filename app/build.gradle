apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion SDK_VERSION
    buildToolsVersion BUILD_TOOL_VERSION

    defaultConfig {
        applicationId "com.bytesforge.linkasanote"
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion SDK_VERSION
        versionCode buildVersionCode()
        versionName VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        resConfigs "en", "ru-rRU"
        //jackOptions {
        //    enabled true
        //}
        buildConfigField "long", "BUILD_TIMESTAMP", System.currentTimeMillis() + "L"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            File singRelease = file('sign_release.properties')
            if (singRelease.exists()) {
                Properties properties = new Properties()
                properties.load(new FileInputStream(singRelease))
                storeFile file(properties['STORE_FILE'])
                storePassword properties['STORE_PASSWORD']
                keyAlias properties['KEY_ALIAS']
                keyPassword properties['KEY_PASSWORD']
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            multiDexEnabled true
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            multiDexEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
        }
        prod
    }

    packagingOptions {
        exclude 'META-INF/CHANGES'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/README'
        exclude 'META-INF/README.txt'
        exclude 'META-INF/rxjava.properties'
    }

    android.variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true);
        }
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            showStandardStreams = true
        }
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':tokenautocomplete')
    compile project(':androidutils')
    //compile project(':nextcloud')

    compile "com.android.support:multidex:${multidexVersion}"
    compile "com.android.support:appcompat-v7:${supportLibraryVersion}"
    compile "com.android.support:support-v13:${supportLibraryVersion}"
    compile "com.android.support:design:${supportLibraryVersion}"
    compile "com.android.support:preference-v14:${supportLibraryVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibraryVersion}"
    compile "com.android.support:cardview-v7:${supportLibraryVersion}"
    compile "com.android.support.constraint:constraint-layout:${constraintLayoutVersion}"

    compile "com.google.guava:guava:${guavaVersion}-android"
    compile "com.github.nextcloud:android-library:${nextcloudLibraryVersion}"
    compile "org.jsoup:jsoup:${jsoupVersion}"

    // RxJava
    compile "io.reactivex.rxjava2:rxjava:${rxjavaVersion}"
    compile "io.reactivex.rxjava2:rxandroid:${rxandroidVersion}"

    // Dagger 2
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    compile "com.google.dagger:dagger:${daggerVersion}"
    provided "javax.annotation:jsr250-api:${jsr250ApiVersion}"

    // Stetho
    debugCompile "com.facebook.stetho:stetho:${stethoVersion}"

    // Local unit tests
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
    testCompile "org.powermock:powermock-module-junit4:${powermockVersion}"
    testCompile "org.powermock:powermock-api-mockito2:${powermockVersion}"

    // Espresso Testing
    androidTestCompile "com.android.support.test.espresso:espresso-core:${espressoVersion}"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:${espressoVersion}"
    androidTestCompile "org.mockito:mockito-core:${mockitoVersion}"
    androidTestCompile "com.linkedin.dexmaker:dexmaker-mockito:${dexmakerVersion}"
    androidTestCompile "com.android.support.test.uiautomator:uiautomator-v18:${uiAutomatorVersion}"

    // Resolve conflicts
    compile "com.google.code.findbugs:jsr305:${findbugsJsr305Version}"
    androidTestCompile "com.android.support:recyclerview-v7:${supportLibraryVersion}"
    androidTestCompile "com.android.support:appcompat-v7:${supportLibraryVersion}"
    androidTestCompile "com.android.support:design:${supportLibraryVersion}"
}
